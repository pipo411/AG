
plugins {
  id 'org.hidetake.ssh' version '2.9.0'
}

ssh.settings {
    knownHosts = allowAnyHosts
}

remotes {
  webServer {
    host = dev_server
    user = user_server
    identity = file(user_home)
  }
}

task deploy {
  doLast {
    ssh.run {
      session(remotes.webServer) {
        execute 'echo Stopping Tomcat service...'
        execute 'sudo service tomcat stop'
        execute "sudo rm -f ${tomcat_webapps}/${war_name}.war", ignoreError: true
        execute "sudo rm -rf ${tomcat_webapps}/${war_name}", ignoreError: true

        execute 'echo Sending war file...'
        put from: "${war_path}/${war_name}.war", into: "/tmp/${war_name}.war"
        execute "sudo cp -f /tmp/${war_name}.war ${tomcat_webapps}/${war_name}.war", ignoreError: true
        remove "/tmp/${war_name}.war"

        execute 'echo Starting Tomcat service...'
        execute 'sudo service tomcat start'
      }
    }
  }
}

